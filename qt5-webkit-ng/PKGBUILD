# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt5-webkit-ng-debug
_orig_pkgname=${pkgname/-debug/}
_qtver=5.212.0
pkgver=${_qtver/-/}
pkgrel=1
arch=(i686 x86_64)
url='https://github.com/annulen/webkit/wiki'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API (maintained fork)'
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
conflicts=(qt5-webkit 'qt5-webkit-ng')
provides=("qt5-webkit-ng=$pkgver" qt5-webkit)
options=("debug" "!strip")
source=("https://github.com/annulen/webkit/archive/qtwebkit-$pkgver-alpha.tar.gz"
        qt5-webkit-icu59.patch qt5-webkit-ng-gcc7.patch qt5-webkit-ng-functional.patch)
sha256sums=('bf8637ad44efd781893c60a932256f435f538bea63333a7f438b897500044d98'
            'e1306e696ad7125fa00083aa966be0ea6fe6de555c9e1d119957f208f258edfe'
            '58ecbd8708f5afe4e974fdcd6c1f32fea61ccc55dc9bb711768f2d0da6963fb8'
            'c85344edcc23930c0190b2f8ec3847bf8bdbbf7f1d4fa877c502e354e5f4233e')

prepare() {
  mkdir -p build

  cd webkit-qtwebkit-$pkgver-alpha
# Fix build with ICU 59 (PLD linux)
  patch -p1 -i ../qt5-webkit-icu59.patch
# Fix build with GCC 7
  patch -p1 -i ../qt5-webkit-ng-gcc7.patch
  patch -p1 -i ../qt5-webkit-ng-functional.patch

  sed -e 's|-Wundef|-Wundef -Wno-expansion-to-defined|' -i Source/cmake/WebKitHelpers.cmake
}

build() {
  cd build  

  cmake ../webkit-qtwebkit-$pkgver-alpha \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base-debug "$pkgdir"/usr/share/licenses/${pkgname}
}
